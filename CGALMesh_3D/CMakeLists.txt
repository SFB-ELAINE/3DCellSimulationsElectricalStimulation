cmake_minimum_required(VERSION 3.1...3.23)
project(Mesh_3_Examples)

add_definitions(-DCGAL_MESH_3_NO_DEPRECATED_SURFACE_INDEX
                -DCGAL_MESH_3_NO_DEPRECATED_C3T3_ITERATORS)

if(CGAL_MESH_3_VERBOSE)
  add_definitions(-DCGAL_MESH_3_VERBOSE)
endif()

find_package(CGAL REQUIRED COMPONENTS ImageIO)

option(CGAL_ACTIVATE_CONCURRENT_MESH_3 "Activate parallelism in Mesh_3" ON)

if(CGAL_ACTIVATE_CONCURRENT_MESH_3 OR "$ENV{CGAL_ACTIVATE_CONCURRENT_MESH_3}")
  message(STATUS "Adding TBB")
  add_definitions(-DCGAL_CONCURRENT_MESH_3)
  find_package(TBB REQUIRED)
  include(CGAL_TBB_support)
  if(NOT TARGET CGAL::TBB_support)
	  message("Notice: TBB support could not be found.")
	  return()
   endif()
endif()

# Use Eigen
find_package(Eigen3 3.1.0 QUIET) #(3.1.0 or greater)
include(CGAL_Eigen3_support)
if(NOT TARGET CGAL::Eigen3_support)
  message("NOTICE: All examples require the Eigen3 library, and will not be compiled.")
  return()
endif()

find_package(VTK QUIET COMPONENTS vtkImagingGeneral vtkIOImage NO_MODULE)
if(VTK_FOUND)
  if(VTK_USE_FILE)
    include(${VTK_USE_FILE})
  endif()
  if("${VTK_VERSION_MAJOR}" GREATER "5" OR VTK_VERSION VERSION_GREATER 5)
    message(STATUS "VTK found")
    if(TARGET VTK::IOImage)
      set(VTK_LIBRARIES VTK::ImagingGeneral VTK::IOImage)
    endif()
  else()
    message(STATUS "VTK version 6.0 or greater is required")
  endif()
else()
  message(STATUS "VTK was not found")
endif()

if(TARGET CGAL::CGAL_ImageIO)
  if(CGAL_ImageIO_USE_ZLIB)
    find_package(ITK NAMES ITK InsightToolkit QUIET COMPONENTS ITKCommon ITKThresholding ITKSmoothing ITKImageIntensity)
    if(ITK_FOUND)
      include(CGAL_ITK_support)
      create_single_source_cgal_program("mesh_3D_image.cpp")
      target_link_libraries(mesh_3D_image PUBLIC CGAL::Eigen3_support CGAL::ITK_support)
      create_single_source_cgal_program("mesh_3D_image_without_features.cpp")
      target_link_libraries(mesh_3D_image_without_features PUBLIC CGAL::Eigen3_support CGAL::ITK_support)
	  create_single_source_cgal_program("mesh_3D_image_with_polyline_features.cpp")
      target_link_libraries(mesh_3D_image_with_polyline_features PUBLIC CGAL::Eigen3_support CGAL::ITK_support)
	  create_single_source_cgal_program("mesh_3D_image_with_weight_and_features.cpp")
      target_link_libraries(mesh_3D_image_with_weight_and_features PUBLIC CGAL::Eigen3_support CGAL::ITK_support)
    else()
	  message(STATUS "NOTICE: mesh_3D_image.cpp needs CGAL_ImageIO, ZLIB and ITK support and will not be compiled.")
    endif()
  endif()
endif()

if(CGAL_ACTIVATE_CONCURRENT_MESH_3 AND TARGET CGAL::TBB_support)
  message(STATUS "NOTICE: Parallel meshing.")
  target_link_libraries(mesh_3D_image PUBLIC CGAL::TBB_support)
  target_link_libraries(mesh_3D_image_without_features PUBLIC CGAL::TBB_support)
endif()
